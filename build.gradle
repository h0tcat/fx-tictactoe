buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        // during build, we depend on the jfxmobile-plugin
        classpath 'org.javafxports:jfxmobile-plugin:1.0.7'
    }
}

apply plugin: 'scala'                       // compile scala sources as well
apply plugin: 'org.javafxports.jfxmobile'   // let jfxmobile-plugin do its dance

mainClassName = 'net.ladstatt.tictactoe.TicTacToeApp'

dependencies {
    compile 'org.scala-lang:scala-library:2.11.7'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenLocal()
    jcenter()
}

jfxmobile {
    ios {
        forceLinkClasses = ['net.ladstatt.**.*']
    }
    android {
        signingConfig {
            // your keystore file which can be created by a program called 'keytool'
            // see https://developer.android.com/tools/publishing/app-signing.html
            storeFile file("/Users/lad/.ssh/ladstatt.keystore")

            // this little trick enables us to commit everything except our passwords
            // to github - like this your private things stay private.
            File propsFile = file("/Users/lad/.ssh/ladstatt.keystore.properties");
            if (propsFile.exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(propsFile))
                storePassword props.getProperty('storePassword')
                keyPassword props.getProperty('keyPassword')
                keyAlias props.getProperty('keyAlias')
            }
        }
        applicationPackage = 'net.ladstatt.tictactoe'
        manifest = 'src/android/AndroidManifest.xml'
    }
}
